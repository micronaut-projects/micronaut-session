plugins {
    id "io.micronaut.build.internal.common"
    id 'org.graalvm.buildtools.native'
}

repositories {
    mavenCentral()
    maven { setUrl("https://s01.oss.sonatype.org/content/repositories/snapshots/") }
}

dependencies {
    api(mn.micronaut.http)

    testAnnotationProcessor(mn.micronaut.inject.java)
    testImplementation(project(":session"))
    testImplementation(mn.reactor)
    testImplementation(mn.micronaut.graal)
    testImplementation(mn.micronaut.http.client)
    testImplementation(mn.micronaut.http.server)
    testImplementation(mn.micronaut.http.server.netty)
    testImplementation(mn.micronaut.test.junit5)
}

configurations {
    all {
        exclude group: 'org.codehaus.groovy'
    }
    nativeImageTestClasspath {
        exclude module: 'groovy-test'
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

tasks.named("check") { task ->
    def graal = ["jvmci.Compiler", "java.vendor.version", "java.vendor"].any {
        System.getProperty(it)?.toLowerCase(Locale.ENGLISH)?.contains("graal")
    }
    if (graal) {
        task.dependsOn("nativeTest")
    }
}

def openGraalModules = [
        "org.graalvm.nativeimage.builder/com.oracle.svm.core.jdk",
        "org.graalvm.nativeimage.builder/com.oracle.svm.core.configure",
        "org.graalvm.sdk/org.graalvm.nativeimage.impl"
]

graalvmNative {
    toolchainDetection = false
    binaries {
        all {
            buildArgs.add("-H:-UseServiceLoaderFeature")
            openGraalModules.each { module ->
                jvmArgs.add("--add-exports=" + module + "=ALL-UNNAMED")
            }
        }
    }
}
